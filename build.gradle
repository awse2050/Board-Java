plugins {
    id 'org.springframework.boot' version '2.6.8'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

// 모든 프로젝트 공통 적용 모듈
allprojects {
    group = 'com.multi'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }
}

// 하위 프로젝트 공통 적용 모듈
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 빌드시 공통 모듈을 각 프로젝트에 포함시키기 위한 설정 - root 프로젝트에는 main 메서드 관련 코드가 없다.
bootJar { enabled = false }
jar { enabled = true }

project(":java-api") {
    dependencies {
        implementation project(":java-core")
    }
}
